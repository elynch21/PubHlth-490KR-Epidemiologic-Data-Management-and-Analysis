/********************************
THIS PROGRAM COMPLETES ACTIVITY FOR
SESSION 16, PH490KR SPRING 2018

EMILY LYNCH 
MARCH 23, 2018
*********************************/

libname ph490kr "C:\Users\emilylynch\Desktop\ph490kr";

data sess16; /*read in data into a temporary dataset that is stored in work and called sess16*/
set ph490kr.vitdsess16; /*this is the dataset being read in*/

*1. create a new variable coding as never, past, or current smokers;
*first, we will look at ev_smk & cur_smk in a table, see *A*. below;

if cur_smk=1 then smoke=2; /*current smokers*/
else if ev_smk=1 & cur_smk=0 then smoke=1; /*past smokers*/
else if ev_smk=0 then smoke=0;

*now check our work, see *B* below;

*2. dichtomize weight at the median, first need to find the median, see *C* below;

if wt_lbs<=136 then wt_med=1;
else if wt_lbs>136 then wt_med=2;
if wt_lbs=. then wt_med=.;
*check our work, see *D* below;

*3. create a new variable calculating BMI;
bmi=wt_lbs/(ht_in*ht_in)*703;
*check our work, see *E* below;

*4. group bmi into who categories;
if (bmi<18.5) then bmicat=1;
else if (bmi>=18.5) & (bmi<25) then bmicat=2;
else if (bmi>=25) & (bmi<30) then bmicat=3;
else if (bmi>=30) then bmicat=4;
else if bmi=. then bmicat=.;
*check our work, see *F* below;

*5. convert height to meters;
ht_m=ht_in*.0254;
*check our work, see *G* below;
run; 

*6. save as a permanent SAS dataset;
data ph490kr.vitdsess16_ECL;
set sess16;
run;

*A*. look at cross-tabulation of ev_smk and cur_smk;

proc freq data=sess16;
table ev_smk*cur_smk /missing;
run;

*B* check our work for creating smoke;
proc freq data=sess16;
table smoke;
run;

proc print data=sess16 (obs=100);
var ev_smk cur_smk smoke;
run;

*C* find the median value of weight;
proc univariate data=sess16;
var wt_lbs;
run;
/*median wt_lbs=136.00*/

*D* check our work in dichotomizing weight;
proc print data=sess16 (obs=50);
var wt_lbs wt_med;
run;

proc sort data=sess16;
by wt_med;
run;
proc univariate data=sess16;
by wt_med;
var wt_lbs;
run;

*E* check work in creating bmi;
proc print data=sess16 (obs=50);
var wt_lbs ht_in bmi;
run;
proc univariate data=sess16;
var bmi;
run;

*F* check work in categorizing BMI;
proc print data=sess16 (obs=75);
var bmi bmicat;
run;

*G* check work in converting height to meters;
proc print data=sess16 (obs=25);
var ht_m ht_in;
run;

***EXTRA: LABEL VARIABLES AND THEIR VALUES***;

proc format library=ph490kr;
value smokef 0="0_never" 1="1_past" 2="2_current";
value wt_medf 1="1:weight <=136 lbs" 2="2:weight >136 lbs";
value bmicatf 1="1_underweight" 2="2_normal weight" 3="3_overweight" 4="4_obese";
run;

options fmtsearch=(ph490kr);

data ph490kr.vitdsess16_ECL; /*note, this code will overwrite my permanent SAS dataset*/
set ph490kr.vitdsess16_ECL;

format smoke smokef. wt_med wt_medf. bmicat bmicatf.;

label smoke="smoke: smoking status";
label wt_med="wt_med: weight relative to median";
label bmi="bmi: BMI, kg/m2";
label bmicat="bmicat: BMI category";
label ht_m="ht_m: height in meters";
run;
proc contents data=ph490kr.vitdsess16_ECL;
run;
